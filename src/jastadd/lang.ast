/**
	Program
**/
Program  ::= Clause* /FormalPredicate*/; 

/**
   Clause 
**/
abstract Clause ::= Heads:InclusiveLiteral*;
Rule:Clause ::=  Body:Literal*;
Fact:Clause;

/**
   Literals 
**/
abstract Literal ::= Predicate:PredicateSymbol;
abstract InclusiveLiteral:Literal;
abstract ExclusiveLiteral:Literal;
abstract BinaryExclusiveTermLiteral:ExclusiveLiteral ::= Left:Expr Right:Expr;

Atom:InclusiveLiteral          ::= Terms:Term*;
EDBLiteral:InclusiveLiteral    ::= PredicateRef FileLoc:Term;
OUTPUTLiteral:InclusiveLiteral ::= Term;
PREDLiteral:InclusiveLiteral   ::= Term /PredicateRef*/;
ATOMLiteral:InclusiveLiteral   ::= Term /PredicateRef*/;
NEGLiteral:ExclusiveLiteral    ::= InclusiveLiteral;

BINDLiteral:ExclusiveLiteral ::= Variable Right:Expr;

// TYPEDECLLiteral:InclusiveLiteral ::= PredicateRef TermList;

EQLiteral:BinaryExclusiveTermLiteral;
NEQLiteral:BinaryExclusiveTermLiteral;
LTLiteral:BinaryExclusiveTermLiteral;
LTELiteral:BinaryExclusiveTermLiteral;
GTLiteral:BinaryExclusiveTermLiteral;
GTELiteral:BinaryExclusiveTermLiteral;

/**
	Expressions
**/
abstract Expr;
//abstract AggregateExpr:Expr;
//CountExpr:AggregateExpr ::= InclusiveLiteral;

abstract BinExpr:Expr ::= Left:Expr Right:Expr;
AddExpr:BinExpr;
SubExpr:BinExpr;
MulExpr:BinExpr;
DivExpr:BinExpr;

/**
	Types
**/
abstract Type:Term;
abstract ConcreteType:Type;

StringType:ConcreteType;
IntegerType:ConcreteType;
PredRefType:ConcreteType;

TypeType:ConcreteType;
VariableType:ConcreteType;
ListType:Type  ::= Type;

/**
    Terms
**/
abstract Term:Expr;
abstract Constant:Term;

Variable:Term           ::= <VAR_ID>;
PredicateRef:Term       ::= <PRED_ID>;
StringConstant:Constant ::= <STRING>;
IntConstant:Constant    ::= <NUMERAL>;
TermList:Term           ::= Terms:Term*;

/**
    PredicateSymbols
**/
FormalPredicate ::= <PRED_ID>;
PredicateSymbol ::= <PRED_ID>;