import java.util.HashSet;
import java.util.HashMap;
import java.util.TreeSet;
import java.util.TreeMap;
import java.util.Set;
import lang.io.SimpleLogger;
import java.util.Collections;

aspect Utility {
    public class MapEntry<Key, Value> {
    	public Key k;
    	public Value v;
    		
    	public MapEntry(Key k, Value v) {
    		this.k = k;
    		this.v = v;
    	}
    }
    
    public abstract class SetConstructor<Value, S extends Set<Value>> {
    	public abstract S construct();
    }
    
    public class TreeSetConstructor<Value> extends SetConstructor<Value, TreeSet<Value>> {
    	private Comparator<Value> comparator;
    	public TreeSetConstructor(Comparator<Value> comparator) {
    		this.comparator = comparator;
    	}
    	public TreeSet<Value> construct() {
    		if(comparator == null)
    			return new TreeSet<Value>();
			else
				return new TreeSet<Value>(comparator);
    	}
    }
    
     public class HashSetConstructor<Value> extends SetConstructor<Value, HashSet<Value>> {
    	public HashSet<Value> construct() {
			return new HashSet<Value>();
    	}
    }
    
    public class MapToSetOf<Key, Value> extends HashMap<Key, Set<Value>> {
    	private SetConstructor<Value, Set<Value>> constructor;
    	public MapToSetOf(SetConstructor<Value, Set<Value>> constructor) {
    		this.constructor = constructor;
    	}
    	public void add(MapEntry<Key, Value> me) {
    		Set<Value> values = get(me.k);
    		if(values == null) {
	    		values = constructor.construct();
	    		values.add(me.v);
	    		put(me.k, values);
    		}else {
    			values.add(me.v);
    		}
    	}
    	
    	
    	public void addAll(Set<MapEntry<Key, Value>> mes) {
    		for(MapEntry<Key, Value> me : mes) {
	    		Set<Value> values = get(me.k);
	    		if(values == null) {
		    		values = constructor.construct();
		    		values.add(me.v);
		    		put(me.k, values);
	    		}else {
	    			values.add(me.v);
	    		}
    		}
    	}
    }
}

aspect Program {
	inh Program Clause.program();
    eq Program.getChild().program() = this;
	eq Program.getChild().hasRecursiveEDB() {
		FormalPredicate fp_edb = formalPredicateMap().get(GlobalNames.EDB_NAME);
		if(fp_edb == null) return false;
		for(PredicateSymbol ps : fp_edb.predicates()) {
			EDBLiteral lit = (EDBLiteral) ps.literal();
			if(lit.getPredicateRef().getPRED_ID().equals(GlobalNames.EDB_NAME)) return true;
		}
		return false;
	}
	
	coll HashSet<PredicateInterface> Program.predicateSymbols() [new HashSet<PredicateInterface>()] with add;
    PredicateSymbol contributes this when true to Program.predicateSymbols();
    PredicateRef    contributes this when true to Program.predicateSymbols();
        
    syn TreeSet<PredicateInterface> Program.uniquePredicateSymbols() {
        TreeSet<PredicateInterface> uniquePredicateSymbols = new TreeSet<PredicateInterface>(Program.predicateComparator);
        uniquePredicateSymbols.addAll(predicateSymbols());
        return uniquePredicateSymbols;
    }
    
    
    syn MapToSetOf<String, PredicateInterface> Program.predicateMap() {
        MapToSetOf<String, PredicateInterface> predicateMap = new MapToSetOf(new HashSetConstructor());
        for(PredicateInterface pred : predicateSymbols()) 
            predicateMap.add(new MapEntry(pred.getPRED_ID(), pred));
        return predicateMap;
    }
    
    public class FormalPredicateMap extends HashMap<String, FormalPredicate> {
    	public void add(MapEntry<String, FormalPredicate> me) { put(me.k, me.v); }
    }
    
    coll FormalPredicateMap Program.formalPredicateMap() [new FormalPredicateMap()] with add;
    Program contributes nta getFormalPredicateList() to Program.formalPredicateMap();
    FormalPredicate contributes new MapEntry(predicateName(), this)
        when true
        to Program.formalPredicateMap();
        
    eq Program.getChild().formalPredicateMap() = formalPredicateMap();
}
aspect Clause {
    inh Clause Literal.clause();
    eq Clause.getChild().clause() = this;
    
    syn boolean Clause.isRule();
    eq Rule.isRule() = true;
    eq Fact.isRule() = false;
}

aspect Literal {
    syn PredicateSymbol Literal.predicate() = getPredicate();
	
 	inh boolean Literal.isFact();
    eq Clause.getChild().isFact()        = false;
    eq Fact.getHeads(int index).isFact() =  true;
    
   	syn boolean Literal.isEDB();
    eq  Literal.isEDB()    = false;
    eq  EDBLiteral.isEDB() = true;
    
    syn boolean Literal.isAtom();
    eq  Literal.isAtom() = false;
    eq  Atom.isAtom()    = true;
    
    syn boolean Literal.isNeg();
    eq  Literal.isNeg()       = false;
    eq  NEGLiteral.isNeg()    = true;
    
    syn boolean Literal.isInclusive();
    eq Literal.isInclusive() = false;
    eq InclusiveLiteral.isInclusive() = true;
    
    syn boolean Literal.hasExtendedSemantics();
    eq Literal.hasExtendedSemantics() = true;
    eq Atom.hasExtendedSemantics()    = false;
    
    inh boolean Literal.isDef();
    eq Clause.getChild().isDef() = false;
    eq Rule.getHeads().isDef()   =  true;
    eq Fact.getHeads().isDef()   =  true;

    inh boolean Literal.isUse();
    eq Clause.getChild().isUse() = false;
    eq Rule.getBody().isUse()    = true;
    
    inh Program PREDLiteral.program();
    syn List PREDLiteral.getPredicateRefList() {
    	List list = new List();
    	for(FormalPredicate fp : program().getFormalPredicates()) {
    		list.add(new PredicateRef(fp.predicateName()));
    	}
    	return list;
    }
    
    inh Program ATOMLiteral.program();
    syn List ATOMLiteral.getPredicateRefList() {
    	List list = new List();
    	for(FormalPredicate fp : program().getFormalPredicates()) {
    		if(fp.literal().isAtom())
    			list.add(new PredicateRef(fp.predicateName()));
    	}
    	return list;
    }
    
    public interface PredefinedLiteral {}
    ATOMLiteral   implements PredefinedLiteral;
    EDBLiteral    implements PredefinedLiteral;
    PREDLiteral   implements PredefinedLiteral;
    OUTPUTLiteral implements PredefinedLiteral;
    TYPEOFLiteral implements PredefinedLiteral;
}

aspect Predicate {
	inh Literal PredicateSymbol.literal();
	inh Literal PredicateRef.literal();
	eq  Literal.getChild().literal() = this;
    
    public interface PredicateInterface {
    	public boolean isRef();
    	public String getPRED_ID();
    }
    PredicateSymbol implements PredicateInterface;
    PredicateRef    implements PredicateInterface;
    
    public boolean PredicateSymbol.isRef() { return false; }
    public boolean PredicateRef.isRef()    { return true; }
    
    syn boolean PredicateInterface.isFact()  = literal().isFact();
    syn boolean PredicateInterface.isEDB()   = literal().isEDB();
    syn boolean PredicateInterface.isUse()   = literal().isUse();
    syn boolean PredicateInterface.isDef()   = literal().isDef();
    syn boolean PredicateInterface.isNeg()   = literal().isNeg();
}

aspect Term {
	inh int Term.index();
	eq Literal.getChild().index() = 0;
	eq Atom.getTerms(int index).index() = index;
	
	inh Literal Term.occuresIn();
	eq  Literal.getChild().occuresIn() = this;
	
	inh boolean Term.isNested();
	eq Program.getChild().isNested() = false;
	eq TermList.getChild().isNested() = true;
	
	syn PseudoTuple TermList.toTuple() = new PseudoTuple(getTermss());
	syn TreeSet<Variable> TermList.variables() {
		TreeSet<Variable> variables = new TreeSet<Variable>(Term.termComparator);
		for(Term t : getTermss()) {
			if(t.isVariable())
				variables.add((Variable)t);
		}
		return variables;
	}
	
	syn TreeSet<Constant> TermList.constants() {
		TreeSet<Constant> constants = new TreeSet<Constant>(Term.termComparator);
		for(Term t : getTermss()) {
			if(t.isConstant())
				constants.add((Constant)t);
		}
		return constants;
	}
}

aspect Expr {
	inh Literal Expr.occuresIn();
}

aspect FormalPredicate {
    syn List Program.getFormalPredicateList() {
    	List list = new List();
    	for(PredicateInterface pred : uniquePredicateSymbols()) {
    		if(!pred.isRef()) {
    			FormalPredicate fp = new FormalPredicate(pred.getPRED_ID());
    			list.add(fp);
    		}
    	}
    	return list;
    }
    inh Program FormalPredicate.program();
    inh boolean FormalPredicate.hasRecursiveEDB();
    inh FormalPredicateMap FormalPredicate.formalPredicateMap();
    
    syn String FormalPredicate.predicateName() = getPRED_ID();
    syn Literal FormalPredicate.literal() = predicates().iterator().next().literal();
    syn HashSet<Literal> FormalPredicate.literals() {
    	HashSet<Literal> literals = new HashSet<Literal>();
    	for(PredicateSymbol ps : predicates()) {
    		literals.add(ps.literal());
    	}
    	return literals;
    }
    
    inh HashSet<PredicateSymbol> FormalPredicate.predicates();
    eq Program.getFormalPredicate(int index).predicates() {
    	HashSet<PredicateSymbol> predicates = new HashSet<PredicateSymbol>();
    	for(PredicateInterface pi :  predicateMap().get(getFormalPredicate(index).getPRED_ID())) {
    		if(!pi.isRef())
    			predicates.add((PredicateSymbol)pi);
    	}
    	return predicates;
    }
    
    inh HashSet<PredicateRef> FormalPredicate.predicateRefs();
    eq Program.getFormalPredicate(int index).predicateRefs() {
    	HashSet<PredicateRef> predicates = new HashSet<PredicateRef>();
    	for(PredicateInterface pi :  predicateMap().get(getFormalPredicate(index).getPRED_ID())) {
    		if(pi.isRef())
    			predicates.add((PredicateRef)pi);
    	}
    	return predicates;
    }
        
    syn FormalPredicate PredicateInterface.formalpredicate() {
    	FormalPredicate fp = literal().clause().program().formalPredicateMap().get(getPRED_ID());
    	if(fp == null) {
	    	SimpleLogger.logger().log("Each PredicateSymbol should belong to a FormalPredicate: " + this, 
	    		SimpleLogger.LogLevel.Level.ERROR);
	    	System.exit(0);
	    	return null; // Needed for JastAdd
    	}
    	return fp;
    }
}

aspect Expression {
	public abstract TreeSet<Variable> Expr.variables();
	syn TreeSet<Variable> BinaryExclusiveTermLiteral.variables() {
		TreeSet<Variable> vars = new TreeSet<Variable>(Term.termComparator);
		vars.addAll(getLeft().variables());
		vars.addAll(getRight().variables());
		return vars;
	}
	
	syn TreeSet<Variable> BinExpr.variables() {
		TreeSet<Variable> vars = new TreeSet<Variable>(Term.termComparator);
		vars.addAll(getLeft().variables());
		vars.addAll(getRight().variables());
		return vars;
	}
	
	syn TreeSet<Variable> Term.variables()     = new TreeSet<Variable>(Term.termComparator);
	syn TreeSet<Variable> Variable.variables() {
		TreeSet<Variable> vars = new TreeSet<Variable>(Term.termComparator);
		vars.add(this);
		return vars;
	}
	
	
	public abstract TreeSet<Constant> Expr.constants();
	syn TreeSet<Constant> BinaryExclusiveTermLiteral.constants() {
		TreeSet<Constant> constants = new TreeSet<Constant>(Term.termComparator);
		constants.addAll(getLeft().constants());
		constants.addAll(getRight().constants());
		return constants;
	}
	
	syn TreeSet<Constant> BinExpr.constants() {
		TreeSet<Constant> constants = new TreeSet<Constant>(Term.termComparator);
		constants.addAll(getLeft().constants());
		constants.addAll(getRight().constants());
		return constants;
	}
	
	syn TreeSet<Constant> Term.constants()     = new TreeSet<Constant>(Term.termComparator);
	syn TreeSet<Constant> Constant.constants() {
		TreeSet<Constant> constants = new TreeSet<Constant>(Term.termComparator);
		constants.add(this);
		return constants;
	}
}

aspect Types {
	public TreeMap<FormalPredicate, PseudoTuple> Program.typeMap = new TreeMap<FormalPredicate, PseudoTuple>(FormalPredicate.formalPredicateComparator);

	public static final Type Type.typeType    = new TypeType();
	public static final Type Type.stringType  = new StringType();
	public static final Type Type.integerType = new IntegerType();
	public static final Type Type.predRefType = new PredRefType();
	public static final Type Type.varType     = new VariableType();
	public static final Type Type.litTypeType = new ListType(typeType);
	public static final Type Type.unknownType = new UnknownType();
	
	syn Type Term.type()      =  Type.typeType;
	eq UnknownType.type()     =  Type.unknownType;
	eq Type.type()            =  Type.typeType;
    eq Variable.type()        =  Type.varType;
    eq PredicateRef.type()    =  Type.predRefType;
    eq StringConstant.type()  =  Type.stringType;
    eq IntConstant.type()     =  Type.integerType;
	eq TermList.type() {
		if(hasTerms()) {
			return new ListType(getTerms(0).type());
		}
		return new ListType(Type.varType);
	}
	
	syn boolean TermList.isWellTyped() = toTuple().toTypeTuple().isUniInfo();
	
	syn boolean Type.isListType()             = false;
	syn boolean ListType.isListType()         = true;
	syn boolean Type.isStringType()           = false;
	syn boolean StringType.isStringType()     = true;
	syn boolean Type.isIntegerType()          = false;
	syn boolean IntegerType.isIntegerType()   = true;
	syn boolean Type.isPredRefType()          = false;
	syn boolean PredRefType.isPredRefType()   = true;
	syn boolean Type.isVariableType()         = false;
	syn boolean VariableType.isVariableType() = true;
	syn boolean Type.isUnknownType()          = false;
	syn boolean UnknownType.isUnknownType()   = true;
	
	syn boolean Term.isString()   = type().isStringType();
	syn boolean Term.isInteger()  = type().isIntegerType();
	syn boolean Term.isPredRef()  = type().isPredRefType();
	syn boolean Term.isVariable() = type().isVariableType();
	syn boolean Term.isConstant() = !isVariable();
	syn boolean Term.isList()     = type().isListType();
	syn boolean Term.isUnknown()  = type().isUnknownType();
	
	syn PseudoTuple PredefinedLiteral.actualType();
	eq  ATOMLiteral.actualType()   = new PseudoTuple(new List(getTerm().type()));
	eq  OUTPUTLiteral.actualType() = new PseudoTuple(new List(getTerm().type()));
	eq  PREDLiteral.actualType()   = new PseudoTuple(new List(getTerm().type()));
	eq  EDBLiteral.actualType()    = new PseudoTuple(new List(Type.predRefType, getFileLoc().type()));
	eq  TYPEOFLiteral.actualType() = new PseudoTuple(new List(Type.predRefType, getTermList().type()));
	
	syn PseudoTuple PredefinedLiteral.type();
	eq  ATOMLiteral.type()   = new PseudoTuple(new List(Type.predRefType));
	eq  OUTPUTLiteral.type() = new PseudoTuple(new List(Type.predRefType));
	eq  PREDLiteral.type()   = new PseudoTuple(new List(getTerm().type()));
	eq  EDBLiteral.type()    = new PseudoTuple(new List(Type.predRefType, Type.stringType));
	eq  TYPEOFLiteral.type() = new PseudoTuple(new List(Type.predRefType, Type.litTypeType));
	
	syn Set<MapEntry<Integer, Type>> PredefinedLiteral.predefinedTypeContribution() {
		HashSet<MapEntry<Integer, Type>> type_binding = new HashSet<MapEntry<Integer, Type>>();
		for(int i = 0; i != type().arity(); ++i)
			type_binding.add(new MapEntry(i, (Type)type().coord(i)));
		return type_binding;
	}
	
	syn String PredefinedLiteral.typeErrorMsg() = "Expected: " + type() + " but got: " + actualType() + " for " + predicate().getPRED_ID();
	syn boolean PredefinedLiteral.satisfiedType() {
		if(type().arity() != actualType().arity()) return false;
		for(int i = 0; i != type().arity(); ++i) {
			Type t1 = (Type)type().coord(i);
			Type t2 = (Type)actualType().coord(i);
			if(t2.isVariableType()) continue;
			if(Type.typeComparator.compare(t1, t2) != 0) return false;
		}
		return true;
	}
}
